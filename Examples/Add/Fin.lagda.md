# Fun with finites!

This chapter is one step in a journey to construct machine-verified hardware design in a simple, principled manner.

We'll start with addition on statically bounded natural numbers, as provided by the [`Data.Fin`](https://agda.github.io/agda-stdlib/Data.Fin.html) module in [the Agda standard library](https://github.com/agda/agda-stdlib).
(Most of the functionality is re-exported from [`Data.Fin.Base`](https://agda.github.io/agda-stdlib/Data.Fin.Base.html).)
The defining module calls these types `Fin n` (for `n : â„•`), but we'll rename them to `ğ”½ n` for the code below.

## Some preliminaries

First declare our module and import needed functionality from other modules:

```agda
module Examples.Add.Fin where

open import Data.Product using (_,_; uncurry)
open import Data.Fin as ğ”½ hiding (_+_) renaming (Fin to ğ”½)
open import Data.Fin.Properties
open import Data.Nat as â„• -- hiding (_+_; _*_)
open import Data.Nat.Properties
open import Relation.Binary.PropositionalEquality
open â‰¡-Reasoning

open import Categorical.Raw hiding (uncurry)
open import Functions
open import Categorical.Arrow Function
```

`Data.Fin` provides a way to increase a number's bound:

```agdaQ
inject+ : âˆ€ {m} n â†’ ğ”½ m â†’ ğ”½ (m â„•.+ n)
```

(Decreasing is trickier.)
`Data.Fin.Properties` tells us that `inject+` only changes the upper bound, while leaving value of that number unchanged:

```agdaQ
toâ„•-inject+ : âˆ€ {m} n (i : ğ”½ m) â†’ toâ„• i â‰¡ toâ„• (inject+ n i)
```

It will be convenient to use a tweaked signature for `inject+`, and to reverse the direction of `toâ„•-inject+`.

```agda
inject+â€² : âˆ€ {m} n â†’ ğ”½ m â†’ ğ”½ (n â„•.+ m)
inject+â€² {m} n j = subst ğ”½ (+-comm m n) (inject+ n j)

toâ„•-subst : âˆ€ {m n} {eq : m â‰¡ n} {i : ğ”½ m} â†’ toâ„• (subst ğ”½ eq i) â‰¡ toâ„• i
toâ„•-subst {eq = refl} = refl

toâ„•-inject+â€² : âˆ€ {m} n (j : ğ”½ m) â†’ toâ„• (inject+â€² n j) â‰¡ toâ„• j
toâ„•-inject+â€² {m} n j = trans toâ„•-subst (sym (toâ„•-inject+ n j))
```

## Adding two numbers

A bounded number `a : ğ”½ n` can be any of `0, ..., n - 1`.
If we add `a : ğ”½ m` to `b : ğ”½ n`, so `a â‰¤ m - 1` and `b â‰¤ n - 1` and thus `a + b â‰¤ m + n - 2`, i.e., has type `ğ”½ (m + n - 1)`.

Well, not exactly, because `â„•` has no negatives and so does not have subtraction in the way we might expect.
Instead, we'll require `m > 0` (although we could instead require `n > 0`).
We could tweak addition to ask for a proof that `m > 0`, but we'd need to make the result more complex as well.
Instead, we can choose a *simpler* type:

```agda
infixl 6 _âŠ¹_
_âŠ¹_ : âˆ€ {m n} â†’ ğ”½ (suc m) â†’ ğ”½ n â†’ ğ”½ (m + n)
_âŠ¹_ {m}      zero   j = inject+â€² m j
_âŠ¹_ {suc _} (suc i) j = suc (i âŠ¹ j)
```

The name of this function suggests that it implements addition, and indeed it does, in the following sense:

```agda
toâ„•-âŠ¹ : âˆ€ {m n} (i : ğ”½ (suc m)) (j : ğ”½ n) â†’ toâ„• (i âŠ¹ j) â‰¡ toâ„• i + toâ„• j
toâ„•-âŠ¹ {m} zero j = toâ„•-inject+â€² m j
toâ„•-âŠ¹ {suc _} (suc i) j rewrite toâ„•-âŠ¹ i j = refl
```

Let's consider the *meaning* of an `ğ”½` value to be the corresponding `â„•`, as given by `toâ„•`.
Then `toâ„•-âŠ¹` says that the meaning of an sum of `ğ”½` values is the sum of the meanings of those values.
The property has a sort of rhyme to it that may sound familiar if you've seen abstract algebra and various examples of *homomorphisms*.

## Packaging it all up to go

We now have five crucial pieces of information:

1.  an *implementation* (`_âŠ¹_`),
2.  a specification (`_+_`), and
3.  a proof of their consistency with respect to
4.  a mapping of implementation input to specification input and
5.  a mapping of implementation output to specification output.

These five pieces are all aspects of a single, meaningful assembly, so let's package them to be convenient to take with us and relate to other such assemblies.
Parts 4 and 5 are about the inputs and outputs and their semantic relationship, so we'll make them the domain and codomain of the assembly, i.e., its interface.
Parts 1, 2, and 3 become the details behind that interface:

```agda
âŠ¹â‡‰ : âˆ€ {m n} â†’ toâ„• {suc m} âŠ— toâ„• {n} â‡‰ toâ„• {m + n}
âŠ¹â‡‰ = mk (uncurry _âŠ¹_) (uncurry _+_) (uncurry toâ„•-âŠ¹)
```

*To do:* define `mkâ€²` to take curried ops, and use in place of `mk` & `uncurry`.

The symbol "`_â‡‰_`" was chosen to suggest a kind of mapping, and belongs to a category such that

*   *objects* (the sorts of inputs and outputs for the category) are data mappings (parts 4 & 5 above); and
*   *morphisms* (the connections/mappings in the category) are pairs of functions (parts 1 and 2 above)---which can really be morphisms from *any* category---that satisfy a "commuting diagram" (part 3 above).

This construction is known as an "[arrow category](https://en.wikipedia.org/wiki/Comma_category#Arrow_category)".

## A dance for three

The `â„•` and `ğ”½` types are *unary* representations, built up from `zero` by repeated applications of `suc`(cessor), as defined by Giuseppe Peano in the late 19th century.
This representation is convenient for reasoning but computationally inefficient in size of representation and cost of arithmetic operations.

In [positional number systems](https://en.wikipedia.org/wiki/Positional_notation) (such as base ten or base two), representations are succinct, and operations are efficient---at the cost of some complexity.
For this reason, we will work our way toward implementing positional systems, defining their meanings via `ğ”½`, which itself is defined via its meaning `â„•`.
We could relate positional systems to `â„•` directly, but there are useful insights to be gained in each step of the journey.
Giving each our focused attention fosters our understanding and appreciation of the jewels we encounter.

When we add two digits (whether in base ten or base two), the result can be too large to denote with a single digit.
For this reason, digit addition produces not only a digit but a "carry-out" value.
No matter what the base, the carry-out is either zero or one, which is to say it has type `ğ”½ 2`, or a "bit", not a digit.
(Digits and bits only coincide in base two.)

When we move *leftward* from digit to digit (since we write the least significant digit on the right and most significant on the left), we "carry out" the carry-out bit into the next digit addition, where it becomes a "carry-in" bit of the next (more significant) digit addition.

In this way, digit addition becomes "a dance for three" (as Carlo Rovelli [says](https://www.goodreads.com/book/show/55801224-helgoland) of quantum entanglement and relative information), not two:


```agda
addğ”½â‚€ : âˆ€ {m n} â†’ ğ”½ 2 Ã— ğ”½ m Ã— ğ”½ n â†’ ğ”½ (m + n)
addğ”½â‚€ (cáµ¢ , a , b) = cáµ¢ âŠ¹ a âŠ¹ b
```

Note how `addğ”½â‚€` replaces the `ğ”½ (suc m)` argument to `_âŠ¹_` by a `ğ”½ 2` and a `ğ”½ m`.
These two arguments then get added to yield `ğ”½ (suc m)` (since `2 â‰¡ suc (suc zero)`), which is then added to a `ğ”½ n` to get a `F (m + n)`.

We'll want to know that `addğ”½â‚€` correctly implements something and what that something is, so let's repeat our packaging game.
A natural meaning is adding three unfettered natural numbers (not troubling them or ourselves with bounds), which we can prove correct and tie up in a neat package:

```agda
addâ„• : â„• Ã— â„• Ã— â„• â†’ â„•
addâ„• (c , a , b) = c + a + b

toâ„•-addğ”½â‚€ : âˆ€ {m n} â†’ toâ„• âˆ˜ addğ”½â‚€ {m}{n} â‰— addâ„• âˆ˜ (toâ„• âŠ— toâ„• âŠ— toâ„•)
toâ„•-addğ”½â‚€ (cáµ¢ , a , b) rewrite toâ„•-âŠ¹ (cáµ¢ âŠ¹ a) b | toâ„•-âŠ¹ cáµ¢ a = refl

addğ”½â‡‰â‚€ : âˆ€ {m n} â†’ toâ„• âŠ— toâ„• {m} âŠ— toâ„• {n} â‡‰ toâ„•
addğ”½â‡‰â‚€ = mk addğ”½â‚€ addâ„• toâ„•-addğ”½â‚€
```

This time the correctness condition (the type of `toâ„•-addğ”½`) in given in succinct, point-free style, using sequential composition (`_âˆ˜_`), parallel composition (`_âŠ—_`), and existential equality of functions (`_â‰—_`).

When reading the definitions above, it helps to know that `_+_` is left-associative, while `_Ã—_`, `_,_`, and `_âŠ—_` are all right-associative.

Now note that each aspect of `addğ”½â‡‰â‚€` is made from the corresponding component of `âŠ¹â‡‰`, using essentially the same recipe:

*   Left-associate `(cáµ¢ , a , b)` to `((cáµ¢ , a) , b)`.
*   Add the first pair, yielding `(cáµ¢ + a , b)`.
*   Add the result, yielding `(cáµ¢ + a) + b`.

Using categorical operations, we can thus define `addğ”½â‡‰` directly via `âŠ¹â‡‰` rather than via ingredients of `âŠ¹â‡‰`:

```agda
addğ”½â‡‰ : âˆ€ {m n} â†’ toâ„• {2} âŠ— toâ„• {m} âŠ— toâ„• {n} â‡‰ toâ„• {m + n}
addğ”½â‡‰ = âŠ¹â‡‰ âˆ˜ first âŠ¹â‡‰ âˆ˜ assocË¡
```

*Whee!*
We've used the `Category` and `Cartesian` instances for comma categories (including their arrow category specialization) to combine our implementation-specification-proof packages, both in sequence and in parallel.
Those two instances encapsulate the knowledge of how to perform these two kinds of compositions and a few other useful operations as well.

::: aside
*To do*: define a cartesian category of equality proofs, and rewrite `addâ„•`, `addğ”½`, `toâ„•-addğ”½` (renamed "`addâ‰¡`"), *and* `addğ”½â‡‰` all in the same form.
:::

## Adding many numbers

Next, let's extend from two summands (and carry-in) to any number, collected in a vector.
Things are about to get wild, but I promise you that they'll calm down soon.

```agda
open import Data.Vec

addğ”½sâ‚€ : âˆ€ {k i m} â†’ ğ”½ (k + i) Ã— Vec (ğ”½ m) k â†’ ğ”½ (k * m + i)
addğ”½sâ‚€ {zero} (cáµ¢ , []) = cáµ¢
addğ”½sâ‚€ {suc k}{i}{m} (cáµ¢ , a âˆ· as) =
  subst ğ”½ eq (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as))
 where
   eq : k * m + (i + m) â‰¡ suc k * m + i
   eq = begin
          k * m + (i + m)
        â‰¡âŸ¨ cong (k * m +_) (+-comm i m) âŸ©
          k * m + (m + i)
        â‰¡âŸ¨ sym (+-assoc (k * m) m i) âŸ©
          k * m + m + i
        â‰¡âŸ¨ cong (_+ i) (+-comm (k * m) m) âŸ©
          m + k * m + i
        â‰¡âŸ¨âŸ©
          suc k * m + i
        âˆ

addsâ‚€ : âˆ€ {k} â†’ â„• Ã— Vec â„• k â†’ â„•
addsâ‚€ = uncurry (foldl _ _+_)

toâ„•-addğ”½sâ‚€ : âˆ€ {k i m} â†’ toâ„• âˆ˜ addğ”½sâ‚€ {k}{i}{m} â‰— addsâ‚€ âˆ˜ (toâ„• âŠ— map toâ„•)
toâ„•-addğ”½sâ‚€ {zero } {i} {m} (cáµ¢ , []) rewrite +-identityÊ³ (toâ„• cáµ¢) = refl
toâ„•-addğ”½sâ‚€ {suc k} {i} {m} (cáµ¢ , a âˆ· as) =
  begin
    toâ„• (addğ”½sâ‚€ (cáµ¢ , a âˆ· as))
  â‰¡âŸ¨âŸ©
    toâ„• (subst ğ”½ _ (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as)))
  â‰¡âŸ¨ toâ„•-subst âŸ©
    toâ„• (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as))
  â‰¡âŸ¨ toâ„•-addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as) âŸ©
    addsâ‚€ (toâ„• (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a)) , map toâ„• as)
  â‰¡âŸ¨ cong (Î» z â†’ addsâ‚€ (z , map toâ„• as)) toâ„•-subst âŸ©
    addsâ‚€ (toâ„• (cáµ¢ âŠ¹ a) , map toâ„• as)
  â‰¡âŸ¨âŸ©
    foldl _ _+_ (toâ„• (cáµ¢ âŠ¹ a)) (map toâ„• as)
  â‰¡âŸ¨ cong (Î» z â†’ foldl _ _+_ z (map toâ„• as)) (toâ„•-âŠ¹ cáµ¢ a) âŸ©
    foldl _ _+_ (toâ„• cáµ¢ + toâ„• a) (map toâ„• as)
  â‰¡âŸ¨âŸ©
    foldl _ _+_ (toâ„• cáµ¢) (map toâ„• (a âˆ· as))
  â‰¡âŸ¨âŸ©
    addsâ‚€ (toâ„• cáµ¢ , map toâ„• (a âˆ· as))
  âˆ

addğ”½sâ‡‰â‚€ : âˆ€ {k i m} â†’ toâ„• {k + i} âŠ— map {n = k} (toâ„• {m}) â‡‰ toâ„• {k * m + i}
addğ”½sâ‡‰â‚€ = mk addğ”½sâ‚€ addsâ‚€ toâ„•-addğ”½sâ‚€
```

Phew!
With considerable effort, we made it.

Unfortunately, math and code are not things we put behind us when written.
In addition to purchase cost, we now have an ongoing paid subscription to complexity :grimacing:.
We must reason through it over and over---both individually and collectively---as we build from here.

## Seeking simplicity

The definitions above are far too complicated for my tastes.
Let's instead look for ways to build up `addğ”½sâ‡‰` from `âŠ¹â‡‰` *compositionally*, as we did we rewrote `addğ”½â‚€` as `addğ”½`.
Let's look for more decomposable formulations.

First, try changing the carry-in to account for being partway into a summation, having accumulated `j` addends with `k` more to go.

```agda
addğ”½sâ‚ : âˆ€ {j k m} â†’ ğ”½ (j * m + k) Ã— Vec (ğ”½ m) k â†’ ğ”½ ((j + k) * m)
addğ”½sâ‚ {j} {zero } {m} (cáµ¢ , [])
  rewrite +-identityÊ³ j | +-identityÊ³ (j * m) = cáµ¢
addğ”½sâ‚ {j} {suc k} {m} (cáµ¢ , a âˆ· as) =
   subst ğ”½ eqâ‚ƒ (addğ”½sâ‚ {suc j}{k}{m} (cáµ¢â€² , as))
 where
   eqâ‚ : j * m + suc k â‰¡ suc (j * m + k)
   eqâ‚ = +-suc (j * m) k
   eqâ‚‚ : (j * m + k) + m â‰¡ suc j * m + k
   eqâ‚‚ = trans (+-comm (j * m + k) m) (sym (+-assoc m (j * m) k))
   eqâ‚ƒ : (suc j + k) * m â‰¡ (j + suc k) * m
   eqâ‚ƒ = cong (_* m) (sym (+-suc j k))
   cáµ¢â€² : ğ”½ (suc j * m + k)
   cáµ¢â€² = subst ğ”½ eqâ‚‚ (subst ğ”½ eqâ‚ cáµ¢ âŠ¹ a)
```

Still not as simple as I want. :frowning:

Here's an idea: rewrite `addsâ‚€` (the specification) in categorical style.
Then imitate for the `ğ”½` version and its correctness proof.
Then replace them all with a single package.

First, write out the left fold explicitly, switching from `Vec` to `V` (made of standard products):

```agda
open import Data.Unit

addsâ‚ƒ : âˆ€ k â†’ â„• Ã— V â„• k â†’ â„•
addsâ‚ƒ zero (cáµ¢ , tt) = cáµ¢
addsâ‚ƒ (suc k) (cáµ¢ , a , as) = addsâ‚ƒ k (cáµ¢ + a , as)
```

Now switch to categorical language:

```agda
addsâ‚„ : âˆ€ k â†’ â„• Ã— V â„• k â†’ â„•
addsâ‚„  zero   = unitoráµ‰Ê³
addsâ‚„ (suc k) = addsâ‚„ k âˆ˜ first (uncurry _+_) âˆ˜ assocË¡
```

Overall: we have `unitoráµ‰Ê³ âˆ˜ first âŸ¨+âŸ© âˆ˜ assocË¡ âˆ˜ â‹¯ âˆ˜ first âŸ¨+âŸ© âˆ˜ assocË¡`, where `âŸ¨+âŸ© = uncurry _+_`.

Next define *one step* of `addğ”½s`.

```agda
addğ”½á¶œ-suc : âˆ€ {j k m : â„•}
          â†’ ğ”½ (suc k + j * m) Ã— V (ğ”½ m) (suc k)
          â†’ ğ”½ (k + suc j * m) Ã— V (ğ”½ m) k
addğ”½á¶œ-suc {j}{k}{m} rewrite sym (+-comm (j * m) m) | sym (+-assoc k (j * m) m) =
  first (uncurry _âŠ¹_) âˆ˜ assocË¡
```

Then use `addğ”½á¶œ-suc` to redefine `addğ”½s`:

```agda
addğ”½sâ‚ƒ : âˆ€ {j k m} â†’ ğ”½ (k + j * m) Ã— V (ğ”½ m) k â†’ ğ”½ ((k + j) * m)
addğ”½sâ‚ƒ {j}{zero } = unitoráµ‰Ê³
addğ”½sâ‚ƒ {j}{suc k}{m} = idâ‰¡ eq âˆ˜ addğ”½sâ‚ƒ {suc j}{k} âˆ˜ addğ”½á¶œ-suc {j}
 where
   eq : ğ”½ ((k + suc j) * m) â‰¡ ğ”½ ((suc k + j) * m)
   eq rewrite +-suc k j = refl
   -- eq = cong (Î» i â†’ ğ”½ (i * m)) (+-suc k j)
```

Much simpler!
I think we're getting somewhere.

I just added `idâ‰¡` as a definition (not field) in the `Category` class, as an alternative to `subst` and `rewrite`:

```agdaQ
  idâ‰¡ : (aâ‰¡b : a â‰¡ b) â†’ a â‡¨ b
  idâ‰¡ refl = id
```

Hm! `addğ”½á¶œ-suc` is a *dependently typed state transition function*
Correspondingly, `addğ”½sâ‚ƒ` is almost the dependently typed execution of the corresponding Mealy machine, but it generates the final state instead of the intermediate outputs.
I guess a better description is a *dependently typed left fold*.

We could eliminate `idâ‰¡ eq` here with the help of a somewhat hairy `subst`.
Alternatively, try `rewrite`.
After a few attempts, I came up with the following:

```agda
addğ”½sâ‚„ : âˆ€ {j k m} â†’ ğ”½ (k + j * m) Ã— V (ğ”½ m) k â†’ ğ”½ ((k + j) * m)
addğ”½sâ‚„ {j}{zero }{m} = unitoráµ‰Ê³
addğ”½sâ‚„ {j}{suc k}{m} rewrite sym (cong (_* m) (+-suc k j)) =
  addğ”½sâ‚„ {suc j}{k}{m} âˆ˜ addğ”½á¶œ-suc {j}
```

Without the `cong`, type-checking failed.
Maybe it needed just a bit more context to avoid some harmful rewrites.

Keep both `addğ”½sâ‚ƒ` and `addğ”½sâ‚„` for now, and evaluate their merits in usage.

From `Data.Vec.Base`:
```agdaQ
foldl : âˆ€ {a b} {A : Set a} (B : â„• â†’ Set b) {m} â†’
        (âˆ€ {n} â†’ B n â†’ A â†’ B (suc n)) â†’
        B zero â†’
        Vec A m â†’ B m
foldl b _âŠ•_ n []       = n
foldl b _âŠ•_ n (x âˆ· xs) = foldl (Î» n â†’ b (suc n)) _âŠ•_ (n âŠ• x) xs
```

This pattern doesn't seem quite general enough, since we're simultaneously decreasing `k` and increasing `j`.
I bet `addğ”½sâ‚„` could be rephrased.
