---
title: Composing correct constructions categorically
author: Conal Elliott
---

# Composing correct constructions categorically

This chapter is one step in a journey to construct machine-verified hardware design in a simple, principled manner.

We'll start with addition on statically bounded natural numbers, as provided by the [`Data.Fin`](https://agda.github.io/agda-stdlib/Data.Fin.html) module in [the Agda standard library](https://github.com/agda/agda-stdlib).
(Most of the functionality is re-exported from [`Data.Fin.Base`](https://agda.github.io/agda-stdlib/Data.Fin.Base.html).)
The defining module calls these types `Fin n` (for `n : â„•`), but we'll rename them to `ğ”½ n` for the code below.

## Some preliminaries

First declare our module and import needed functionality from other modules:

```agda
module Examples.Add.Fin where

open import Data.Unit
open import Data.Product using (_,_; uncurry)
open import Data.Fin as ğ”½ hiding (_+_) renaming (Fin to ğ”½)
open import Data.Fin.Properties
open import Data.Nat as â„•
open import Data.Nat.Properties
open import Relation.Binary.PropositionalEquality
open â‰¡-Reasoning
open import Data.Vec

open import Categorical.Raw hiding (uncurry)
open import Functions
open import Categorical.Arrow Function
```

`Data.Fin` and `Data.Fin.Properties` provide a way to increase a number's bound, while assuring us that its value remains the same:

```agdaQ
inject+ : âˆ€ {m} n â†’ ğ”½ m â†’ ğ”½ (m â„•.+ n)

toâ„•-inject+ : âˆ€ {m} n (i : ğ”½ m) â†’ toâ„• i â‰¡ toâ„• (inject+ n i)
```

It will be convenient to tweak the signature of `inject+` and to reverse the direction of `toâ„•-inject+`.

```agda
inject+â€² : âˆ€ {m} n â†’ ğ”½ m â†’ ğ”½ (n â„•.+ m)
inject+â€² {m} n j = subst ğ”½ (+-comm m n) (inject+ n j)

toâ„•-subst : âˆ€ {m n} {eq : m â‰¡ n} {i : ğ”½ m} â†’ toâ„• (subst ğ”½ eq i) â‰¡ toâ„• i
toâ„•-subst {eq = refl} = refl

toâ„•-inject+â€² : âˆ€ {m} n (j : ğ”½ m) â†’ toâ„• (inject+â€² n j) â‰¡ toâ„• j
toâ„•-inject+â€² {m} n j = trans toâ„•-subst (sym (toâ„•-inject+ n j))
```

## Adding two numbers

A (bounded) number `a : ğ”½ n` can be any of `0, ..., n - 1`.
If we add `a : ğ”½ m` to `b : ğ”½ n`, then `0 â‰¤ a â‰¤ m - 1` and `0 â‰¤ b â‰¤ n - 1`, so `0 â‰¤ a + b â‰¤ m + n - 2`, i.e., has type `ğ”½ (m + n - 1)`.

Well, not exactly.
`â„•` has no negatives and so does not have subtraction in the way we might expect.
Instead, we'll require `m > 0` (although one might require `n > 0` instead).
We could tweak addition to ask for a proof that `m > 0`, but we'd need to make the result more complex as well.
Instead, we can choose a *simpler* type:

```agda
infixl 6 _âŠ¹_
_âŠ¹_ : âˆ€ {m n} â†’ ğ”½ (suc m) â†’ ğ”½ n â†’ ğ”½ (m + n)
_âŠ¹_ {m}      zero   j = inject+â€² m j
_âŠ¹_ {suc _} (suc i) j = suc (i âŠ¹ j)
```

The name of this function suggests that it implements addition, and indeed it does, in the following sense:

```agda
toâ„•-âŠ¹ : âˆ€ {m n} (i : ğ”½ (suc m)) (j : ğ”½ n) â†’ toâ„• (i âŠ¹ j) â‰¡ toâ„• i + toâ„• j
toâ„•-âŠ¹ {m} zero j = toâ„•-inject+â€² m j
toâ„•-âŠ¹ {suc _} (suc i) j rewrite toâ„•-âŠ¹ i j = refl
```

Let's consider the *meaning* of an `ğ”½` value to be the corresponding `â„•`, as given by `toâ„•`.
Then `toâ„•-âŠ¹` says that *the meaning of a sum of `ğ”½` values is the sum of the meanings of those values*.
The property has a sort of rhyme to it that may sound familiar if you've seen abstract algebra and its various examples of *homomorphisms*.

## Packaging it all up to go

We now have five crucial pieces of information:

1.  an *implementation* (`_âŠ¹_`),
2.  a specification (`_+_`), and
3.  a proof of their consistency with respect to
4.  a mapping of implementation input to specification input and
5.  a mapping of implementation output to specification output.

These five pieces are all aspects of a single, meaningful assembly, so let's wrap them into a convenient package to take with us and relate to other such assemblies.
Parts 4 and 5 are about the inputs and outputs and their semantic relationship and so will become the domain and codomain of the assembly, i.e., its interface.
Parts 1, 2, and 3 become the details behind that interface:

```agda
âŠ¹â‡‰ : âˆ€ {m n} â†’ toâ„• {suc m} âŠ— toâ„• {n} â‡‰ toâ„• {m + n}
âŠ¹â‡‰ = mk (uncurry _âŠ¹_) (uncurry _+_) (uncurry toâ„•-âŠ¹)
```

::: aside
*To do:* define `mkâ€²` to take curried operations, and use in place of `mk` & `uncurry`.
:::

The symbol "`_â‡‰_`" was chosen to suggest a kind of mapping, belonging to a category in which

*   *objects* (the sorts of inputs and outputs for the category) are data mappings (parts 4 & 5 above); and
*   *morphisms* (the connections/mappings in the category) are pairs of functions (parts 1 and 2 above)---which can really be morphisms from *any* category---that satisfy a "commuting diagram" (part 3 above).

This construction is known as an "[arrow category](https://en.wikipedia.org/wiki/Comma_category#Arrow_category)".

## A dance for three

The `â„•` and `ğ”½` types are *unary* representations, built up from `zero` by repeated applications of `suc`(cessor), as defined by Giuseppe Peano in the late 19th century.
This representation is logically convenient but computationally inefficient in size of representation and cost of arithmetic operations.

In [positional number systems](https://en.wikipedia.org/wiki/Positional_notation) (such as base ten or base two), representations are succinct, and operations are efficient---at the cost of some complexity.
For this reason, we will work our way toward implementing positional systems, defining their meanings via `ğ”½`, which itself is defined via its meaning `â„•`.
We could relate positional systems to `â„•` directly, but there are useful insights to be gained in each step of the journey.
By pausing at each step and giving focused attention to our surroundings, we foster understanding and appreciation of the jewels we encounter.

When we add two digits (whether in base ten or base two), the result can be too large to denote with a single digit.
For this reason, digit addition produces not only a digit but an overflow---or "carry-out"---value as well.
No matter what the base, the carry-out is either zero or one, which is to say it is a `ğ”½ 2`, or a "bit", not a digit.
(Digits and bits coincide only in base two.)

When we move *leftward* from digit to digit (since we write the least significant digit on the right and most significant on the left), we "carry out" the carry-out bit into the next digit addition, where it becomes the "carry-in" bit of the next (more significant) digit addition.

In this way, digit addition becomes "a dance for three" (as [Carlo Rovelli says](https://www.goodreads.com/book/show/55801224-helgoland) of quantum entanglement and relative information):

```agda
addğ”½â‚€ : âˆ€ {m n} â†’ ğ”½ 2 Ã— ğ”½ m Ã— ğ”½ n â†’ ğ”½ (m + n)
addğ”½â‚€ (cáµ¢ , a , b) = cáµ¢ âŠ¹ a âŠ¹ b
```

Note how `addğ”½â‚€` replaces the `ğ”½ (suc m)` argument to `_âŠ¹_` by `ğ”½ 2` *and* `ğ”½ m`.
These two arguments are added to yield `ğ”½ (suc m)` (since `2 â‰¡ suc (suc zero)`), which is then added to a `ğ”½ n` to get a `F (m + n)`.

We'll want to know that `addğ”½â‚€` correctly implements something and what that something is, so let's repeat our packaging game.
A natural meaning is adding three unfettered natural numbers (not troubling them or ourselves with bounds), which we can prove correct and package up:

```agda
addâ„• : â„• Ã— â„• Ã— â„• â†’ â„•
addâ„• (c , a , b) = c + a + b

toâ„•-addğ”½â‚€ : âˆ€ {m n} â†’ toâ„• âˆ˜ addğ”½â‚€ {m}{n} â‰— addâ„• âˆ˜ (toâ„• âŠ— toâ„• âŠ— toâ„•)
toâ„•-addğ”½â‚€ (cáµ¢ , a , b) rewrite toâ„•-âŠ¹ (cáµ¢ âŠ¹ a) b | toâ„•-âŠ¹ cáµ¢ a = refl

addğ”½â‡‰â‚€ : âˆ€ {m n} â†’ toâ„• âŠ— toâ„• {m} âŠ— toâ„• {n} â‡‰ toâ„•
addğ”½â‡‰â‚€ = mk addğ”½â‚€ addâ„• toâ„•-addğ”½â‚€
```

This time the correctness condition (the type of `toâ„•-addğ”½`) is given in succinct, point-free style, using sequential composition (`_âˆ˜_`), parallel composition (`_âŠ—_`), and existential equality on functions (`_â‰—_`).

When reading the definitions above, it helps to know that `_+_` is left-associative, while `_Ã—_`, `_,_`, and `_âŠ—_` are all right-associative.

Now note that each aspect of `addğ”½â‡‰â‚€` is made from the corresponding component of `âŠ¹â‡‰`, using essentially the same recipe:

*   Left-associate `(cáµ¢ , (a , b))` to `((cáµ¢ , a) , b)`.
*   Add the first pair, yielding `(cáµ¢ + a , b)`.
*   Add the result, yielding `(cáµ¢ + a) + b`.

Using categorical operations, we can thus define `addğ”½â‡‰` directly via `âŠ¹â‡‰` rather than defining the ingredients of `addğ”½â‡‰` via the ingredients of `âŠ¹â‡‰`:

```agda
addğ”½â‡‰ : âˆ€ {m n} â†’ toâ„• {2} âŠ— toâ„• {m} âŠ— toâ„• {n} â‡‰ toâ„• {m + n}
addğ”½â‡‰ = âŠ¹â‡‰ âˆ˜ first âŠ¹â‡‰ âˆ˜ assocË¡
```

We've used the `Category` and `Cartesian` instances for comma categories (including their arrow category specialization) to compose our implementation-specification-proof packages, both in sequence and in parallel.
(There's only a hint of the parallel here yet, but eventually there will be much more.)
Those two instances encapsulate the knowledge of how to perform these two foundational kinds of compositions and a few other useful operations as well.

::: aside
*To do*: define a cartesian category of equality proofs, and rewrite `addâ„•`, `addğ”½`, `toâ„•-addğ”½` (renamed "`addâ‰¡`"), *and* `addğ”½â‡‰` all in the same form.

Hm. It doesn't seem possible to make equality proofs cartesian, since the cartesian operations relate different types.
Even monoidal can allow at most isomorphism.

There is a simpler alternative, namely a category of morphism equivalences, which I think is a subcategory of an arrow category.
:::

## Adding many numbers

Next, let's extend our ambition from two summands (and carry-in) to any number of them, collected in a vector.
To simplify matters, assume that vector is uniformly bounded, i.e., all addends other than carry-in have the same bound.

::: aside
This assumption lets us use a uniform vector type (all elements having the same type and hence bound).
Dependent types are sufficiently expressive for nonuniform vectors, however, and I bet that the exploration below generalizes in lovely ways.
:::

One motivation to reach for adding many numbers is simply the challenge---to up our game.
As we'll see, though, interesting and useful insights will emerge from the effort.
The essential challenge is in expressing clearly the bounds involved.

For any `m : â„•`, the sum of two values bounded by `m` is at most `2 * (m - 1) â‰¡ 2 * m - 2`.
Well, not exactly (as we noted above), because `â„•` lacks a suitable notion of subtraction (i.e., one that has the relationship to substitution on `â„¤` that makes reasoning easy and useful).
We got around that problem neatly by introducing a carry-in bit, which happens to be needed for efficient, positional number systems.

When we're adding not just two but three `m`-bounded numbers, the sum is at most `3 * m - 3`.
When adding `k` such numbers, the sum is at most `k * m - k`.
Oh dear---subtraction again :scream_cat:.

Can we extend the carry-in trick to find our way back to type simplicity?
Yes, by allowing the carry-in to be at most `k - 1`, i.e., to have type `Fin k`.
Then the sum is at most `(k * m - k) + (k - 1) â‰¡ k * m - 1`, i.e., has type `Fin (k * m)`:

```agdaQ
addğ”½s : âˆ€ {k m} â†’ ğ”½ k Ã— Vec (ğ”½ m) k â†’ ğ”½ (k * m)
```

::: aside
What we've discovered here is that the carry-in bound has nothing to do with the addend (e.g., digit) bounds, but rather is the number of addends.
As a special case, for a single "summand" (`k â‰¡ 1`), the carry-in type is `ğ”½ 1`, which contains only `zero`.
The result has the same bound and the same value as the lonely summand, since `(ğ”½ 1 Ã— Vec (ğ”½ m) 1 â†’ ğ”½ (1 * m)) â‰… (ğ”½ m â†’ ğ”½ m)`.
An even weirder special case is no summands at all, for which the carry-bit type `ğ”½ 0` is uninhabited.
This case "works", too, since `(ğ”½ 0 Ã— Vec (ğ”½ m) 0 â†’ ğ”½ (0 * m)) â‰… âŠ¥ â†’ âŠ¥`, which has just one inhabitant.
:::

As we move rightward through the vector (which, confusingly, corresponds to moving *lefward* in our familiar positional numeric notations), the "carry-in" value grows by absorbing successive summands as .
For this reason, while initially of type `ğ”½ k`, we will have to leave room to grow (even as `k` shrinks).
As a first guess, let's try the following type, adding a new parameter `i` to help bound the accumulator.

```agda
addğ”½sâ‚€ : âˆ€ {k i m} â†’ ğ”½ (k + i) Ã— Vec (ğ”½ m) k â†’ ğ”½ (k * m + i)
```

This signature gives us the flexibility needed to accommodate summand accumulation and will specialize to `addğ”½s` above when `i â‰¡ 0` (with help from `+-identityÊ³`).

Things are about to get wild, but I promise you that they'll calm down soon.
Feel free to read the next few definitions carefully or breeze through them, as pleases you.
(Agda's type-checker already pointed out my many mistakes and eventually approved the revision below.)

```agda
addğ”½sâ‚€ {zero} (cáµ¢ , []) = cáµ¢

addğ”½sâ‚€ {suc k}{i}{m} (cáµ¢ , a âˆ· as) =
  subst ğ”½ eq (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as))
 where
   eq : k * m + (i + m) â‰¡ suc k * m + i
   eq = begin
          k * m + (i + m)
        â‰¡âŸ¨ cong (k * m +_) (+-comm i m) âŸ©
          k * m + (m + i)
        â‰¡âŸ¨ sym (+-assoc (k * m) m i) âŸ©
          k * m + m + i
        â‰¡âŸ¨ cong (_+ i) (+-comm (k * m) m) âŸ©
          m + k * m + i
        â‰¡âŸ¨âŸ©
          suc k * m + i
        âˆ

addâ„•sâ‚€ : âˆ€ {k} â†’ â„• Ã— Vec â„• k â†’ â„•
addâ„•sâ‚€ = uncurry (foldl _ _+_)

toâ„•-addğ”½sâ‚€ : âˆ€ {k i m} â†’ toâ„• âˆ˜ addğ”½sâ‚€ {k}{i}{m} â‰— addâ„•sâ‚€ âˆ˜ (toâ„• âŠ— map toâ„•)
toâ„•-addğ”½sâ‚€ {zero } {i} {m} (cáµ¢ , []) rewrite +-identityÊ³ (toâ„• cáµ¢) = refl
toâ„•-addğ”½sâ‚€ {suc k} {i} {m} (cáµ¢ , a âˆ· as) =
  begin
    toâ„• (addğ”½sâ‚€ (cáµ¢ , a âˆ· as))
  â‰¡âŸ¨âŸ©
    toâ„• (subst ğ”½ _ (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as)))
  â‰¡âŸ¨ toâ„•-subst âŸ©
    toâ„• (addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as))
  â‰¡âŸ¨ toâ„•-addğ”½sâ‚€ (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a) , as) âŸ©
    addâ„•sâ‚€ (toâ„• (subst ğ”½ (+-assoc k i m) (cáµ¢ âŠ¹ a)) , map toâ„• as)
  â‰¡âŸ¨ cong (Î» z â†’ addâ„•sâ‚€ (z , map toâ„• as)) toâ„•-subst âŸ©
    addâ„•sâ‚€ (toâ„• (cáµ¢ âŠ¹ a) , map toâ„• as)
  â‰¡âŸ¨âŸ©
    foldl _ _+_ (toâ„• (cáµ¢ âŠ¹ a)) (map toâ„• as)
  â‰¡âŸ¨ cong (Î» z â†’ foldl _ _+_ z (map toâ„• as)) (toâ„•-âŠ¹ cáµ¢ a) âŸ©
    foldl _ _+_ (toâ„• cáµ¢ + toâ„• a) (map toâ„• as)
  â‰¡âŸ¨âŸ©
    foldl _ _+_ (toâ„• cáµ¢) (map toâ„• (a âˆ· as))
  â‰¡âŸ¨âŸ©
    addâ„•sâ‚€ (toâ„• cáµ¢ , map toâ„• (a âˆ· as))
  âˆ

addğ”½sâ‡‰â‚€ : âˆ€ {k i m} â†’ toâ„• {k + i} âŠ— map {n = k} (toâ„• {m}) â‡‰ toâ„• {k * m + i}
addğ”½sâ‡‰â‚€ = mk addğ”½sâ‚€ addâ„•sâ‚€ toâ„•-addğ”½sâ‚€
```

Phew!
With considerable effort, we made it.

Math and code, however, are not things we put behind us once written.
In addition to the purchase cost, we now have an ongoing paid subscription to complexity :grimacing:.
We must reason through this mess over and over---both individually and collectively---as we build from here.

Or cancel our subscription, learn from experience, and try something else.

## Seeking simplicity

The definitions above are far too complicated for my tastes; perhaps for yours as well.
Seeking simplicity, we can look for ways to build up `addğ”½sâ‡‰` from `âŠ¹â‡‰` *compositionally*, as we did when rewriting `addğ”½â‚€` as `addğ”½`.
Following our earlier success, let's pursue the following plan:

*   Rewrite `addâ„•s` (part 2 of the packing list above) in categorical style.
*   Imitate the new form in the `ğ”½` counterpart (part 1) and correctness proof (part 3) for appropriate data interpretations (parts 4 & 5).
*   Combine all five parts into a single package.
*   Review what we've done, and replace it all with a single categorical recipe that assembles the package compositionally.

First, switch from `Vec` to `V` (an isomorphic, recursively defined type made with standard products), and write out the left fold explicitly:

```agda
addâ„•sâ‚ : âˆ€ k â†’ â„• Ã— V â„• k â†’ â„•
addâ„•sâ‚ zero (cáµ¢ , tt) = cáµ¢
addâ„•sâ‚ (suc k) (cáµ¢ , a , as) = addâ„•sâ‚ k (cáµ¢ + a , as)
```

Now switch to categorical language:

```agda
addâ„•sâ‚‚ : âˆ€ k â†’ â„• Ã— V â„• k â†’ â„•
addâ„•sâ‚‚  zero   = unitoráµ‰Ê³
addâ„•sâ‚‚ (suc k) = addâ„•sâ‚‚ k âˆ˜ first (uncurry _+_) âˆ˜ assocË¡
```

We could have used `exl` (left projection) for the `zero` case, but `unitoráµ‰Ê³` (right unit elimination) emphasizes that we are discarding only the value `tt : âŠ¤`, which contains no information.

::: aside
Unitors are available in monoidal categories, which do not provide for duplicating or destroying information.
Non-cartesian, monoidal categories include reversible computations, which suggest a remedy for the [unavoidably heat-generating](https://en.wikipedia.org/wiki/Landauer%27s_principle) (diabatic) nature of the current dominant paradigm of irreversible computing.
:::

Unrolling the loop, we get `unitoráµ‰Ê³ âˆ˜ first âŸ¨+âŸ© âˆ˜ assocË¡ âˆ˜ â‹¯ âˆ˜ first âŸ¨+âŸ© âˆ˜ assocË¡`, where `âŸ¨+âŸ© = uncurry _+_`.

Can we imitate this form for `ğ”½`?

We can start by defining *one step* of `addğ”½s`, going from the sum of `k` addends (in addition to carry-in) to the sum of `k+1`.
For additional precision, we can replace the accumulated `i` from above with `j * m`.

```agda
addğ”½á¶œ-suc : âˆ€ {j k m : â„•}
          â†’ ğ”½ (suc k + j * m) Ã— V (ğ”½ m) (suc k)
          â†’ ğ”½ (k + suc j * m) Ã— V (ğ”½ m) k
addğ”½á¶œ-suc {j}{k}{m} rewrite sym (+-comm (j * m) m) | sym (+-assoc k (j * m) m) =
  first (uncurry _âŠ¹_) âˆ˜ assocË¡
```

Now, we can use `addğ”½á¶œ-suc` to redefine `addğ”½s`, this time imitating the simple, left-fold form of `addsâ„•`:

```agda
addğ”½sâ‚ : âˆ€ {j k m} â†’ ğ”½ (k + j * m) Ã— V (ğ”½ m) k â†’ ğ”½ ((k + j) * m)
addğ”½sâ‚ {j}{zero } = unitoráµ‰Ê³
addğ”½sâ‚ {j}{suc k}{m} = idâ‰¡ eq âˆ˜ addğ”½sâ‚ {suc j}{k} âˆ˜ addğ”½á¶œ-suc {j}
 where
   eq : ğ”½ ((k + suc j) * m) â‰¡ ğ”½ ((suc k + j) * m)
   eq rewrite +-suc k j = refl
```

(We could phrase that last line more explicitly as `eq = cong (Î» i â†’ ğ”½ (i * m)) (+-suc k j)`.)

As we march forward, `j` counts how many vector elements we've met and gratefully absorbed, while `k` counts how many more we can gleefully anticipate.
As `j` ascends from `zero`, `k` descends to `zero`, always in perfect balance â˜¯.
Ultimately, we offer a well-digested summary of our encounters.

Much simpler!
I think we're getting somewhere.

The `idâ‰¡` function  used here (a definition---not field---in the `Category` class) provides an alternative to `subst` and `rewrite`:

```agdaQ
  idâ‰¡ : a â‰¡ b â†’ a â‡¨ b
  idâ‰¡ refl = id
```

::: aside
We can eliminate `idâ‰¡ eq` here with the help of a somewhat hairy `subst` or via `rewrite`.
After a few attempts, I came up with the following:

```agda
addğ”½sâ‚‚ : âˆ€ {j k m} â†’ ğ”½ (k + j * m) Ã— V (ğ”½ m) k â†’ ğ”½ ((k + j) * m)
addğ”½sâ‚‚ {j}{zero }{m} = unitoráµ‰Ê³
addğ”½sâ‚‚ {j}{suc k}{m} rewrite sym (cong (_* m) (+-suc k j)) =
  addğ”½sâ‚‚ {suc j}{k}{m} âˆ˜ addğ”½á¶œ-suc {j}
```

Without the `cong`, type-checking failed.
Maybe it needed just a bit more context to avoid some harmful uses.
:::

::: aside
It feels right to me that this `addğ”½sâ‚` definition looks like a *dependently typed left fold*, since its purpose is to implement the simply typed left fold in the definition of `addâ„•s`, while refining (the simply typed) `â„•` into (the dependently typed) `ğ”½`.

The `foldl` we used above from `Data.Vec.Base` already does have a dependent type:
```agdaQ
foldl : âˆ€ {a b} {A : Set a} (B : â„• â†’ Set b) {m} â†’
        (âˆ€ {n} â†’ B n â†’ A â†’ B (suc n)) â†’
        B zero â†’
        Vec A m â†’ B m
foldl b _âŠ•_ n []       = n
foldl b _âŠ•_ n (x âˆ· xs) = foldl (Î» n â†’ b (suc n)) _âŠ•_ (n âŠ• x) xs
```

This pattern doesn't seem quite general enough, since we're simultaneously decreasing `k` and increasing `j`.
On the other hand, maybe `addğ”½sâ‚` could be rephrased to fit comfortably.
:::

## Still to come

There are more places to visit on our journey.
Some we can imagine from here:

*   Packaging `addâ„•s`, `addğ”½s`, and a corresponding proof into `addğ”½sâ‡‰`
*   Revealing carry-out and its use in decomposing addition
*   Efficient addition via positional number representations
*   Various recipes for sequential and parallel addition and their hybrids
*   Multiplication
*   Circuit design and verification

These adventures and more await us.
